
provide:
   describe, xdescribe
   it, xit
   before
   after
   beforeEach
   afterEach
   expect
   expectError
   assert
   asserts

require:
   util -> inspect


standard{info, #variable! name, descr, body} =
   let it = info.mark{`@`}
   arg = if{descr, {descr}, {}}
   ```
   [^name]{^*arg} with async: {} ->
      let ^it = this
      ^body
   ```

macro describe{`{^descr, ^body}`} =
   standard{@, .describe, descr, body}

macro it{`{^descr, ^body}`} =
   standard{@, .it, descr, body}

macro before{`{^body}`} =
   standard{@, .before, null, body}

macro after{`{^body}`} =
   standard{@, .after, null, body}

macro beforeEach{`{^body}`} =
   standard{@, .beforeEach, null, body}

macro afterEach{`{^body}`} =
   standard{@, .afterEach, null, body}


macro xdescribe{`{^descr, ^body}`} =
   standard{@, .xdescribe, descr, body}

macro xit{`{^descr, ^body}`} =
   standard{@, .xit, descr, body}



makeInfo{parts} =

   sanitize{inspect! chain} =
      @split{"\n"}
      if{@length > 1, @0 + "...", @0}
      if{@length > 80, @slice{0, 80}, @}

   len = Math.max{*lens} where lens = parts each {x, _} -> x.length
   pad{x} =
      x + [new Array{len + 1 - x.length}.join{" "}]

   xs.join{"\n"} where
      xs = parts each {x, y} ->
         S'  {pad! x} : {sanitize! y}'

makeError{name, message, parts} =
   e = new Error{}
   e.name = name
   e.message = message + "\n" + makeInfo{parts}
   e.stack = e.name + ": " + e.message
   e

ExpectationError{expectation, parts} =
   makeError{.ExpectationError, msg, parts} where
      msg = S'`{parts[0][0]}` is not `{expectation.trim{}}`'

ExpectationErrorE{e, expectation, parts} =
   makeError{.ExpectationError, msg, parts} where
      msg = match e:
         null? -> S'`{parts[0][0]}` failed to raise an error (expected: `{expectation.trim{}}`)'
         else -> S'`{parts[0][0]}` raised the wrong error (got: `{e}` but expected: `{expectation}`)'

AssertionError{parts} =
   makeError{.AssertionError, msg, parts} where
      msg = S'Assertion failed: `{parts[0][0]}`'


gettext{match} =
   {=> location} when location -> location.text{}
   else -> "<unknown>"

decompose{@, expr} =
   decls = {}
   body = #multi{}
   important = {}
   other = {}
   seen = new Set{{"<unknown>"}}

   dispatch{node, tmp, match} =
      do:
         txt = gettext{node}
         if seen.has{txt}:
            return
         else:
            seen.add{txt}
         x = #data{#value{txt}, tmp}
      true? -> important.push{x}
      else -> other.push{x}

   mktemp{} =
      temp = #variable{@gensym{"temp"}}
      decls.push with `var ^temp = "<not computed>"`
      temp

   res = helper{expr, 2} where helper{match x, importance, fnpos = false} =

      #variable{_} ->
         dispatch{x, x, importance > 0 and not fnpos}
         x

      #send{fn, #array{*args}} ->
         temp = mktemp{}
         dispatch{x, temp, importance > 0}
         body.push with `set-var ^temp = ^[#send{fn2, #data{*args2}}]` where
            fn2 = #final{fn} ;;helper{fn, importance - 1, true}
            args2 = args each arg -> helper{arg, importance - 1}
         temp

      #send{fn, arg} ->
         temp = mktemp{}
         dispatch{x, temp, importance > 0}
         body.push with `set-var ^temp = ^[#send{fn2, arg2}]` where
            fn2 = #final{fn} ;;helper{fn, importance - 1}
            arg2 = helper{arg, importance - 1}
         temp

      #value or #array or #object or #void ->
         #final{x}

      else ->
         temp = mktemp{}
         dispatch{x, temp, importance > 0}
         body.push with `set-var ^temp = ^[#final{x}]`
         temp

   {= res, = decls, = body, = important, = other}


macro asserts{`{^name, ^body}` or `{^body}` and name is null} =
   #multi! #multi{*stmts} = body
   let {it, describe, assert} = @mark{`it`, `describe`, `assert`}
   tests = #multi{*___} with stmts each
      `^label => ^stmt` ->
         `[^it] [^label]: ^assert ^stmt`
      stmt ->
         `[^it] [^= @gettext{stmt}]: ^assert ^stmt`
   if name:
      `[^describe]{^name, ^tests}`
   else:
      tests


macro{AssertionError} assert{`{^expr}` or expr} =
   ae = @deps.AssertionError
   if @context == #expr{.multi}:
      return #nostep{@form}
   {=> res, => decls, => body, => important, => other} =
      decompose{@, @expand{@context, expr}}
   ```
   ^*decls
   ^body
   match ^res:
      true? ->
         undefined
      else ->
         throw [^ae]{{^*important, ^*other}}
   ```

macro{ExpectationError} expect{`{^pattern, ^expr}`} =
   if @context == #expr{.multi}:
      return #nostep{@form}
   {=> res, => decls, => body, => important, => other} =
      decompose{@, @expand{@context, expr}}
   ```
   ^*decls
   match ^body:
      ^pattern ->
         undefined
      else ->
         throw [^ @deps.ExpectationError]{^=gettext{pattern}, {^*important, ^*other}}
   ```

macro{ExpectationErrorE} expect-error{match} =
   `{^pattern, ^expr}` or `{^expr}` and pattern is `Error` ->
      patt = match pattern:
         `[^x]?` -> pattern
         else -> `[^pattern]?`
      if @context == #expr{.multi}:
         return #nostep{@form}

      ```
      var ok = false
      try:
         rval = ^expr
      catch ^patt:
         ok = true
      catch e:
         throw [^ @deps.ExpectationErrorE]{e, ^=gettext{pattern}, {{^=gettext{expr}, rval}}}
      if not ok:
         throw [^ @deps.ExpectationErrorE]{null, ^=gettext{pattern}, {{^=gettext{expr}, rval}}}
      ```
